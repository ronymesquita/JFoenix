apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java-library'
apply plugin: 'org.openjfx.javafxplugin'
apply plugin: 'maven-publish'

apply from: rootProject.file('gradle/license.gradle')

javafx {
  version = '11.0.2'
  modules = [ 'javafx.controls', 'javafx.graphics' ]
}

compileJava {
    inputs.property('moduleName', moduleName)
    options.compilerArgs += [
            '--add-exports=javafx.graphics/com.sun.javafx.scene=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.scene.text=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.geom=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.stage=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.util=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.scene.traversal=com.jfoenix',
            '--add-exports=javafx.controls/com.sun.javafx.scene.control=com.jfoenix',
            '--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=com.jfoenix',
            '--add-exports=javafx.controls/com.sun.javafx.scene.control.inputmap=com.jfoenix',
            '--add-exports=javafx.base/com.sun.javafx.binding=com.jfoenix',
            '--add-exports=javafx.base/com.sun.javafx.event=com.jfoenix',
            '--add-exports=javafx.graphics/com.sun.javafx.text=com.jfoenix'
    ]
}

// pom file name
ext {
    moduleName = 'com.jfoenix'
    pomFilePath = "${project.projectDir}/tmp/pom.xml"
    pomFile = file(pomFilePath)
}

group = 'com.jfoenix'
version = '9.0.10'


configurations {
    pom
}

task sourcesJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    excludes = ['**/*.html', 'META-INF/**']

    options.use = true
    options.splitIndex = true
    options.encoding = 'UTF-8'
    options.author = true
    options.version = true
    options.windowTitle = "$project.name $project.version API"
    options.docTitle = "$project.name $project.version API"
    options.links = ['https://docs.oracle.com/en/java/javase/11/docs/api/',
                     'https://openjfx.io/javadoc/11/']
}

task javadocJar(type: Jar) {
    group 'Build'
    description 'An archive of the javadoc'
    classifier 'javadoc'
    from javadoc.destinationDir
}

// execute jar tasks
jar.finalizedBy sourcesJar
jar.finalizedBy javadocJar

// summarize artifacts
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
    if (pomFile.exists()) {
        pom pomFile
    }
}

jar {
    exclude('com/jfoenix/android/**')

    bnd ('Bundle-Name': 'JFoenix',
      //  'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.9',
        'Bundle-Description': 'JavaFX Material Design Library'
    )
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'jfoenix'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'JFoenix'
                description = 'JFoenix is an open source Java library, that implements Google Material Design using Java components'
                url = 'http://www.jfoenix.com'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'https://github.com/jfoenixadmin/JFoenix.git'
                    developerConnection = 'git@github.com:jfoenixadmin/JFoenix.git'
                    url = 'https://github.com/jfoenixadmin/JFoenix'
                }
            }
        }
    }
}

